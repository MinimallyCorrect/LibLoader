plugins {
	id 'java'
	id 'maven'
}

group = 'me.nallar.libloader'
version = '0.1-SNAPSHOT'

// Extended project information
ext.projectName = 'LibLoaderChained'
ext.inceptionYear = '2017'
ext.packaging = 'jar'
ext.url = 'https://github.com/nallar/LibLoaderChained'
ext.description = 'Lib Loader'
ext.organization = 'nallar.me'

// ci information
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

repositories {
	jcenter()
}

dependencies {
	testCompile 'junit:junit:4.12'
	compileOnly "org.projectlombok:lombok:1.16.16"
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'
}

tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		encoding = 'UTF-8'
		compilerArgs << "-Xlint:all" << "-Xlint:-classfile" << "-Xlint:-processing"
	}
}

tasks.withType(Jar) {
	baseName = project.name
	manifest.mainAttributes(
		"Built-By": System.properties['user.name'],
		"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
		"Implementation-Title": name,
		"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
		"Implementation-Vendor": url
	)
}

task innerJar(type: Jar, dependsOn: classes)

innerJar {
	classifier 'inner'
	from sourceSets.main.output
	exclude 'net'
	exclude 'generated'
	manifest.mainAttributes(
		"FMLCorePlugin": "me.nallar.libloader.LibLoader"
	)
}

jar.dependsOn(innerJar)

jar {
	exclude 'net'
	exclude 'me/nallar/libloader/LibLoaderChained*'
	exclude 'generated'
	from innerJar.archivePath
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}
