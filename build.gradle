plugins {
	id 'java'
	id 'maven'
}

group = 'org.minimallycorrect.libloader'
version = '0.1-SNAPSHOT'

ext.inceptionYear = '2017'
ext.packaging = 'jar'
ext.url = 'https://github.com/MinimallyCorrect/LibLoader'
ext.description = 'Lib Loader'
ext.organization = 'minimallycorrect.org'

String calcVersion() {
	def vs = version.toString()
	def snapshot = false
	if (vs.endsWith("-SNAPSHOT")) {
		snapshot = true
		vs = vs.replace("-SNAPSHOT", "")
	}
	vs = vs + '.' + (System.currentTimeMillis()/100000.0)
	if (snapshot) {
		vs = vs + "-SNAPSHOT"
	}
	return vs
}

// version file
def versionFile = file(buildDir.toString() + '/LibLoader.version')
versionFile.getParentFile().mkdirs()
versionFile.text = calcVersion()

// ci information
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

repositories {
	jcenter()
	maven { url "https://libraries.minecraft.net/" }
}

dependencies {
	testCompile 'junit:junit:4.12'
	compileOnly "org.projectlombok:lombok:1.16.16"
	compileOnly 'net.minecraft:launchwrapper:1.12'
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'
}

tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		encoding = 'UTF-8'
		compilerArgs << "-Xlint:all" << "-Xlint:-classfile" << "-Xlint:-processing"
	}
}

tasks.withType(Jar) {
	baseName = project.name
	manifest.mainAttributes(
		"Built-By": System.properties['user.name'],
		"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
		"Implementation-Title": name,
		"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
		"Implementation-Vendor": url
	)
}

task innerJar(type: Jar, dependsOn: classes)

innerJar {
	inputs.property("versionfile", versionFile.text)
	classifier 'inner'
	from sourceSets.main.output
	exclude 'net'
	exclude 'generated'
	from versionFile
	manifest.mainAttributes(
		"FMLCorePlugin": "org.minimallycorrect.libloader.LibLoader"
	)
}

jar.dependsOn(innerJar)

jar {
	inputs.property("versionfile", versionFile.text)
	exclude 'net'
	exclude 'org/minimallycorrect/libloader/LibLoaderChained*'
	exclude 'generated'
	from versionFile
	from innerJar.archivePath
	rename (file(innerJar.archivePath).name, "LibLoader.jar")
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}
